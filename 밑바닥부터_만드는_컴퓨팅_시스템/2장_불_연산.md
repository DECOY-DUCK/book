## 2.불 연산

>이장의 목표는 ALU를 구현하는 것이다. ALU는 컴퓨터의 모든 산술 및 논리 연산을 이루는 핵심 칩이다.  
> 
>[이것부터 하면 많은 도움이 된다.](https://nandgame.com/)
>                 

### **이장에서 구현해야 하는 것들**

- 가산기
- ALU

### 가산기

- 반가산기 half-adder
- 전가산기 full-adder
- 가산기 adder

가산기를 만들기 위해서는 반가산기와 전가산기가 필요하다 그러한 이유는 2진수의 덧셈을 생각해 보면 쉽게 알 수 있다.

![image](https://user-images.githubusercontent.com/51963264/188137752-1ed9f232-688b-4b38-a8f4-c4123b6d6cd2.png)

4비트 2진수 덧셈을 할때 LSB 에서 MSB 까지 차례대로 덧셈을 한다. 1,0 0,0 0,1의 덧셈일  경우 자리올림 발생하지 않기 때문에 반가산기만으로 덧셈을 표현할 수 있지만 1,1의 경우 자리 올림이 발생하고 그때 전가산기가  필요하기 때문에 가산기를 구현하기 위해서는 반가산기와 전가산기가 필요하다.

### 반가산기

![image](https://user-images.githubusercontent.com/51963264/188144402-f6afa16a-5c54-4fc6-84ca-2efb150d3274.png)

올림이 발생하는 지에 대한 연산 와 덧셈 결과값 반환하는 연산이 필요하다. 

'1 + 1 일때만 자리 올림이 발생한다'를 생각해보면 어떤 논리 게이트를 써야 되는지 알 수 있다.  
0 1 , 1 0 일때만 1을 반환한다. 그러한 논리게이트가 있는지 찾아본다


![image](https://user-images.githubusercontent.com/51963264/188152673-8be5465e-b45d-4cca-9270-ec8669a0d98c.png)


```
CHIP HalfAdder {
    IN a, b;    // 1-bit inputs
    OUT sum,    // Right bit of a + b 
        carry;  // Left bit of a + b

    PARTS:
    Xor(a=a, b=b, out=sum);
    And(a=a, b=b, out=carry);
}

```

### 전가산기

이제 자리올림 비트 까지 고려한 3개의 비트를 연산을 하는 게이트를 만들어야 한다.
반가산기와 논리게이트 조합으로 만들 수 있다.

![image](https://user-images.githubusercontent.com/51963264/188158383-780c6538-1017-4d27-9506-1d39b8c97303.png)


```
CHIP FullAdder {
    IN a, b, c;  // 1-bit inputs
    OUT sum,     // Right bit of a + b + c
        carry;   // Left bit of a + b + c

    PARTS:
    
    HalfAdder(a=a, b=b, sum=d, carry=f);
    HalfAdder(a=d, b=c, sum=sum, carry=g);
    Or(a=f, b=g, out=carry);
}
```

### 가산기

반가산기와 전가산기가 준비되면 가산기를 만들 수 있다. 앞서 보여준 4비트 2진수의 덧셈을 생각해보면 16비트 덧셈도 어렵지 않게 만들수 있다.

```

CHIP Add16 {
    IN a[16], b[16];
    OUT out[16];

    PARTS:
    HalfAdder(a=a[0], b=b[0], sum=out[0], carry=carry0);
    FullAdder(a=a[1], b=b[1], c=carry0, sum=out[1], carry=carry1);
    FullAdder(a=a[2], b=b[2], c=carry1, sum=out[2], carry=carry2);
    FullAdder(a=a[3], b=b[3], c=carry2, sum=out[3], carry=carry3);
    FullAdder(a=a[4], b=b[4], c=carry3, sum=out[4], carry=carry4);
    FullAdder(a=a[5], b=b[5], c=carry4, sum=out[5], carry=carry5);
    FullAdder(a=a[6], b=b[6], c=carry5, sum=out[6], carry=carry6);
    FullAdder(a=a[7], b=b[7], c=carry6, sum=out[7], carry=carry7);
    FullAdder(a=a[8], b=b[8], c=carry7, sum=out[8], carry=carry8);
    FullAdder(a=a[9], b=b[9], c=carry8, sum=out[9], carry=carry9);
    FullAdder(a=a[10], b=b[10], c=carry9, sum=out[10], carry=carry10);
    FullAdder(a=a[11], b=b[11], c=carry10, sum=out[11], carry=carry11);
    FullAdder(a=a[12], b=b[12], c=carry11, sum=out[12], carry=carry12);
    FullAdder(a=a[13], b=b[13], c=carry12, sum=out[13], carry=carry13);
    FullAdder(a=a[14], b=b[14], c=carry13, sum=out[14], carry=carry14);
    FullAdder(a=a[15], b=b[15], c=carry14, sum=out[15], carry=carry15);

}
```
### ALU

가산기와 논리 게이트를 이용해서 `산술 논리 연산 장치 ALU(Arithmetic Logical Unit)`을 만들어 보는 것으로 2장을 마무리 한다. 요구사항이 많고 꽤 복잡하기 때문에 인터페이스를 그려보고 하나씩 구현하면 보다 쉽게 만들 수 있다.

![image](https://user-images.githubusercontent.com/51963264/188170776-91e5ee0e-3f9e-49b3-846e-7e994119a075.png)

공통된 기능을 뽑아서 커스텀 칩을 만드는게 정신건강에 좋다. 여기서는 16비트가 그대로 나올지 반전되서 나올지 또는 0이 나올지 제어하는 칩이 중복으로 쓰이는데 'N notN Zero'라는 칩을 먼저 만드는 것으로 시작하면 쉽게 구현 할수 있다.

![image](https://user-images.githubusercontent.com/51963264/188280576-98b3953a-73d0-4c21-a279-56182a3b3327.png)

```
CHIP  NnotNzero{
    IN a[16],sel,sel2;
    OUT out[16];

PARTS:
    //nx zx 
    Mux16(a=a, b=false, sel=sel, out=select0orX);
    Not16(in=select0orX, out=notselect0orX);
    Mux16(a=select0orX, b=notselect0orX, sel=sel2, out=out);

}
```
1장에서 배운 Mux을 이용해서 조건(zx,nx)에 따라 반환값을 다르게 출력하는 칩을 만든다 그런 다음 ALU의
x[16],zx,nx 와 y[16],yx ,ny 에 대입한다.

그런 다음은 'N notN zero' 칩에서 나온 x y 값을 +,& 연산 한 값을 구한후 mux을 통해 반환하면 된다. 그런식을 반복하면 ALU가 완성된다.

```
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // 입력 x를 0으로
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    //nx zx 
    NnotNzero(a=x,sel=zx,sel2 = nx, out= X);
    //ny zy 
    NnotNzero(a=y,sel=zy,sel2 = ny, out= Y);

    //x+y
    Add16(a=X, b=Y, out=plus);
    //X&Y
    And16(a=X, b=Y, out=and);

    Mux16(a=and, b=plus, sel=f, out=ans);

    //!ans
    Not16(in=ans, out=notans);

    //out or !out
    // Mux16(a=ans, b=notans, sel=no, out=out, out[0..15]= o );
    Mux16(a=ans, b=notans, sel=no, out=out, out[15]=ng, out[0..7]=finalLeft, out[8..15]=finalRight);
    
    //equal
        Or8Way(in=finalLeft, out=zrl);
        Or8Way(in=finalRight, out=zrr);
        Or(a=zrl, b=zrr, out=nzr);
        Not(in=nzr, out=zr);
```